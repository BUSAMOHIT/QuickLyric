apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    jcenter()
    flatDir {
        dirs 'libs'
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
    }
}

//noinspection GroovyAssignabilityCheck
android {
    sourceSets {
        main {
            assets.srcDirs = ['assets']
        }
    }
    compileSdkVersion 23
    buildToolsVersion '23.0.1'

    defaultConfig {
        applicationId "com.geecko.QuickLyric"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 54
        versionName "2.0 RC1"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {

        release {
            storeFile file('../release.keystore')
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //noinspection GroovyMissingReturnStatement
    productFlavors {
        fdroid {
        }
        play {
        }
        promo {
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.+'
    compile 'com.android.support:appcompat-v7:23.+'
    compile 'com.android.support:support-v13:23.+'
    //noinspection GradleCompatible
    compile 'com.android.support:design:22.+' // Design Library v23 is broken
    compile 'com.android.support:cardview-v7:23.+'
    compile 'com.balysv:material-ripple:1.0.2'
    compile 'com.mcxiaoke.volley:library:1.0.+'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.+'
    compile 'org.jraf:android-switch-backport:2.0.1'
    compile 'com.mcxiaoke.viewpagerindicator:library:2.4.1'
    compile 'com.wdullaer:materialdatetimepicker:1.4.2'
    compile 'org:jaudiotagger:2.0.3'
    compile 'org.jsoup:jsoup:1.8.+'
    compile 'ch.acra:acra:4.+'

    /* The pure GPLv3 build */
    //Available here: https://github.com/geecko86/SpotifyApi
    fdroidCompile(name: 'SpotifyApi', ext: 'aar')

    /* Use for promotional screenshots and screen recording */
    promoCompile 'com.android.support:palette-v7:23.+'

    /* Debug & testing */
    testCompile 'junit:junit:4.12'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

}
